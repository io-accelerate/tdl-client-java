plugins {
    // Ref here: https://docs.gradle.org/7.4.1/userguide/publishing_setup.html
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

group = 'io.accelerate'
version = project.version.toString()

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'ch.qos.logback:logback-classic:1.4.12'
    implementation 'org.apache.activemq:activemq-client:6.1.6'

    implementation platform('com.fasterxml.jackson:jackson-bom:2.17.3')
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    
    implementation 'com.google.code.gson:gson:2.8.9'

    testImplementation('org.junit.jupiter:junit-jupiter:5.9.1')
    testImplementation('org.junit.platform:junit-platform-suite:1.8.1')
    testImplementation 'org.hamcrest:hamcrest-all:1.3'

    testImplementation 'io.cucumber:cucumber-java:7.21.0'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.21.0'
    testImplementation 'io.cucumber:cucumber-picocontainer:7.21.0'
}

test {
    useJUnitPlatform()
    systemProperty("cucumber.junit-platform.naming-strategy", "long")
    testLogging {
        events "passed", "skipped", "failed"
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

// Suppress Javadoc warnings
javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// Refs:
// https://stackoverflow.com/a/68505768
publishing {
    publications {
        release(MavenPublication) {
            from components.java
            pom {
                name = 'Java client for TDL system'
                description = 'A Java client that allows users to get up to speed with the TDL system.'
                url = 'https://github.com/io-accelerate/tdl-client-java'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Iulian (Julian) Ghionoiu'
                        email = 'iulian.ghionoiu@gmail.com'
                        organization = 'Iulian Ghionoiu'
                        organizationUrl = 'https://github.com/io-accelerate'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:io-accelerate/tdl-client-java.git'
                    developerConnection = 'scm:git:git@github.com:io-accelerate/tdl-client-java.git'
                    url = 'git@github.com:io-accelerate/tdl-client-java.git'
                }
            }
        }
    }
    repositories {
        maven {
            url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = project.findProperty('mavenCentralUsername')
                password = project.findProperty('mavenCentralPassword')
            }
        }
    }
}

// Signs using gpg-agent
// Key name configured under "~/.gradle/gradle.properties"
//
// Refs:
// https://docs.gradle.org/7.4.1/userguide/signing_plugin.html#sec:using_gpg_agent
signing {
    useGpgCmd()
    sign publishing.publications.release
}