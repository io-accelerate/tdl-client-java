apply plugin: 'java'

//~~~~ Settings for Gradle
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:2.2.8"
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
    }
}

//~~~~ Settings for the source code
sourceCompatibility = 1.8
group = 'ro.ghionoiu'

def specFolder = file("src/test/resources/tdl/client")
def majorMinorVersion = ['sh', '-c', "git --git-dir ${specFolder}/.git describe --all | cut -d '/' -f 2 | tr -d 'v'"]
        .execute().text.trim()
version = "${majorMinorVersion}.${currentPatchVersion}"

repositories {
    mavenCentral()
}


ext.libraries = [
    cucumber: [
       'info.cukes:cucumber-java:1.2.4',
       'info.cukes:cucumber-junit:1.2.4',
       'info.cukes:cucumber-picocontainer:1.2.4',
    ]
]

dependencies {
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'com.google.guava:guava:18.0'
    compile 'org.apache.activemq:activemq-client:5.11.1'

    testCompile libraries.cucumber
    testCompile 'com.google.code.gson:gson:2.3.1'
    testCompile 'org.apache.httpcomponents:httpclient:4.5'
    testCompile ('junit:junit:4.12') {
        exclude module : 'hamcrest-core'
    }
    testCompile ('org.mockito:mockito-core:1.10.19') {
        exclude module : 'hamcrest-core'
    }
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'com.github.stefanbirkner:system-rules:1.11.0'
}

// ~~~~~ Gather coverage

apply plugin: "net.saliman.cobertura"
apply plugin: 'com.github.kt3k.coveralls'

cobertura {
    coverageFormats = ['html', 'xml']
    coverageIncludes = ['.*tdl.client.*']
    coverageExcludes = ['.*tdl.examples.*']
    coverageIgnores = ['.*org.slf4j.Logger.*']
}
test.finalizedBy(project.tasks.cobertura)

//~~~~~ Making the library deployable

/**
 * Prerequisites
 *    - Create package in Bintray
 *
 * Now you can use regular maven upload to push files to you package.
 * The version will be created automatically
 *
 * To push to Maven Central use Bintray sync features:
 *    - Created request for jcenter ( if missing )
 *    - Manually sync the two repos
 *
 * To publish new version use the Bintray UI:
 *    - publish current artifacts
 *    - sync with Maven Central
 */
apply plugin: 'maven'

private String readSystem(String property) {
    System.getenv(property) != null  ? System.getenv(property) : "unset"
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Add the sourceJars to non-extractor modules
artifacts {
    archives sourceJar
    archives javadocJar
}

uploadArchives {
    repositories.mavenDeployer {
        name = 'bintray-julianghionoiu-tdl-client-java'
        repository(id: 'bintray-julianghionoiu-tdl-client-java',
                url: 'https://api.bintray.com/maven/julianghionoiu/maven/tdl-client-java') {
            authentication(userName: readSystem("BINTRAY_USER"), password: readSystem("BINTRAY_KEY"))
        }

        pom.project {
            name 'Java client for TDL system'
            description 'A Java client that allows users to get up to speed with the TDL system.'
            url 'https://github.com/julianghionoiu/tdl-client-java'

            developers {
                developer {
                    name 'Iulian (Julian) Ghionoiu'
                    email 'iulian.ghionoiu@gmail.com'
                    organization 'Iulian Ghionoiu'
                    organizationUrl 'https://github.com/julianghionoiu'
                }
            }

            scm {
                connection 'scm:git:git@github.com:julianghionoiu/tdl-client-java.git'
                developerConnection 'scm:git:git@github.com:julianghionoiu/tdl-client-java.git'
                url 'git@github.com:julianghionoiu/tdl-client-java.git'
            }

            licenses {
                license {
                    name 'GNU General Public License, Version 3.0'
                    url 'http://www.gnu.org/licenses/gpl-3.0.txt'
                }
            }
        }
    }
}