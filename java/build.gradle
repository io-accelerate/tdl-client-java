apply plugin: 'java'

//~~~~ Settings for Gradle
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:2.2.8"
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
    }
}

//~~~~ Settings for the source code
sourceCompatibility = 1.8
group = 'ro.ghionoiu'
version = releaseVersion

repositories {
    mavenCentral()
}

dependencies {
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'com.google.guava:guava:18.0'
    compile 'org.apache.activemq:activemq-client:5.11.1'

    testCompile ('junit:junit:4.12') {
        exclude module : 'hamcrest-core'
    }
    testCompile ('org.mockito:mockito-core:1.10.19') {
        exclude module : 'hamcrest-core'
    }
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'com.github.stefanbirkner:system-rules:1.11.0'
}


//~~~~~ For coverage
apply plugin: "net.saliman.cobertura"
apply plugin: 'com.github.kt3k.coveralls'

cobertura {
    coverageFormats = ['html', 'xml']
    coverageIncludes = ['.*competition.client.*']
    coverageIgnores = ['.*org.slf4j.Logger.*']
}
test.finalizedBy(project.tasks.cobertura)


//~~~~ DEBT: This is a library the executable code should move to examples
apply plugin: 'application'
mainClassName = 'competition.example.AddNumbersExample'
applicationDefaultJvmArgs = ["-Dlogback.configurationFile=logback.xml"]

run {
    standardInput = System.in
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

//~~~~~ Making the library deployable

/**
 * Prerequisites
 *    - Create package in Bintray
 *
 * Now you can use regular maven upload to push files to you package.
 * The version will be created automatically
 *
 * To push to Maven Central use Bintray sync features:
 *    - Created request for jcenter ( response comes in a day )
 */
apply plugin: 'maven'

// Load the secret keys: bintrayUser, bintrayKey
if (file('secret.gradle').exists()) {
    apply from: 'secret.gradle'
}

uploadArchives {
    repositories {
        mavenDeployer {
            name = 'bintray-julianghionoiu-tdl-client-java'
            repository(id: 'bintray-julianghionoiu-osgb36-tdl-client-java',
                       url: 'https://api.bintray.com/maven/julianghionoiu/maven/tdl-client-java') {
                authentication(userName: bintrayUser, password: bintrayKey)
            }
            pom.artifactId = 'tdl-client-java'
        }
    }
}